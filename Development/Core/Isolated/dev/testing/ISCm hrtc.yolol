t=999999 s="" goto 1+:rec
s+="["+(t-:r)+","+(:t/1000)+"]:" :ctr++ goto 3-:rec
o="" :exp="" :ctr-- goto 3+:out
c=s c-=s-- o+=c goto 4+(c==":")
c=o c-=o-- :exp+=c goto 5
goto 7-:out
goto 3
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
dt="station_" at=dt+"hq_imperial_a" bt=dt+"proving_grounds"
ct=dt+"kingdom_outpost_b" dt+="kingdom_outpost_b_2" t=999999 x=1000
:d=d :h=h :q=at :o=o/(w-e) :o2=o2/(w-e) 
if :p==at then w=e y=l e=:t/x l=t-:r-a l2=l-y a=t-:r goto 5 end goto4
:a=a :e=e :q=bt :l=l/(w-e) :l2=l2/(w-e)
if :p==bt then w=f y=m f=:t/x m=t-:r-b m2=m-y b=t-:r goto 7 end goto6
:b=b :f=f :q=ct :m=m/(w-e) :m2=m2/(w-e)
if :p==ct then w=g y=n g=:t/x n=t-:r-c n2=n-y c=t-:r goto 9 end goto8
:c=c :g=g :q=dt :n=n/(w-e) :n2=n2/(w-e)
if :p==dt then w=h y=o h=:t/x o=t-:r-d o2=o-y d=t-:r goto 3 end goto10
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
e= 207744.954 f=-1550111.534 g=486779.292 h=-160699.392 i=-46169.175
j=-140483.070 k=  103669.797 l=799664.698 m=-264673.522 n= -4718.935
o=-230400.908 p=  345153.593 q= 41051.448 r= -43636.344 s= -1606.533
t=:t/1000 u=t-:e v=t-:f a=(:a+(:l+:l2*u)*u)^2 b=(:b+(:m+:m2*v)*v)^2
u=t-:g v=t-:h c=(:c+(:n+:n2*u)*u)^2 d=(:d+(:o+:o2*u)*v)^2
:X=a/e+b/f+c/g+d/h-i :Y=a/j+b/k+c/l+d/m-n :Z=a/o+b/p+c/q+d/r-s goto 4
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
w=1000 x="\nX:" y="\nY:" z="\nZ:" 
:ISAN=x+:X/w*w+y+:Y/w*w+z+:Z/w*w goto 2
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
t=:t/1000 u=t-:e v=t-:f :ca=(:l+:l2*u)*u :cb=(:m+:m2*v)*v
u=t-:g v=t-:h :cc=(:n+:n2*u)*u :cd=(:o+:o2*u)*v goto 1



//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|

d="station_" a=d+"hq_imperial_a" b=d+"proving_grounds"
c=d+"kingdom_outpost_b" d+="kingdom_outpost_b_2" t=999999 x=1000
:q=a
if :p==a then :l=(t-:r-:a)/(:e-:t/x) :e=:t/x :a=t-:r goto5 end goto4
:q=b
if :p==b then :m=(t-:r-:b)/(:f-:t/x) :f=:t/x :b=t-:r goto7 end goto6
:q=c
if :p==c then :n=(t-:r-:c)/(:g-:t/x) :g=:t/x :c=t-:r goto9 end goto8
:q=d 
if :p==d then :o=(t-:r-:d)/(:h-:t/x) :h=:t/x :d=t-:r goto3 end goto10
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
e= 207744.954 f=-1550111.534 g=486779.292 h=-160699.392 i=-46169.175
j=-140483.070 k=  103669.797 l=799664.698 m=-264673.522 n= -4718.935
o=-230400.908 p=  345153.593 q= 41051.448 r= -43636.344 s= -1606.533
t=:t/1000 a=(:a+:l*(t-:e))^2 b=(:b+:m*(t-:f))^2
c=(:c+:n*(t-:g))^2 d=(:d+:o*(t-:h))^2
:X=a/e+b/f+c/g+d/h-i :Y=a/j+b/k+c/l+d/m-n :Z=a/o+b/p+c/q+d/r-s goto 4
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
w=1000 x="\nX:" y="\nY:" z="\nZ:" 
:ISAN=x+:X/w*w+y+:Y/w*w+z+:Z/w*w goto 2
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
x=1000
t=:t/x :ca=:l*(t-:e) :cb=:m*(t-:f) :cc=:n*(t-:g) :cd=:o*(t-:h) goto2



//Global memory map
//a b c d e f g h i j k l m n o p q r s t u v w x y z
//|recvs| |times|       | rate| |REC|   t       |pos|
//                              m t s   





//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
d="station_" a=d+"hq_imperial_a" b=d+"proving_grounds"
c=d+"kingdom_outpost_b" d+="kingdom_outpost_b_2" t=999999 x=1 y=10
:q=a
if :p==a then :l=((t-:r)^2-:a)/(e-:t) :e=:t :a=(t-:r)^2 x/=0 end goto4
:q=b e=:e                                                    
if :p==b then :m=((t-:r)^2-:b)/(f-:t) :f=:t :b=(t-:r)^2 x/=0 end goto6
:q=c f=:f
if :p==c then :n=((t-:r)^2-:c)/(g-:t) :g=:t :c=(t-:r)^2 x/=0 end goto8
:q=d g=:g
if :p==d then :o=((t-:r)^2-:d)/(h-:t) :h=:t :d=(t-:r)^2 x/=0 end gotoy
:q=a h=:h goto 4
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
e= 207744.954 f=-1550111.534 g=486779.292 h=-160699.392 i=-46169.175
j=-140483.070 k=  103669.797 l=799664.698 m=-264673.522 n= -4718.935
o=-230400.908 p=  345153.593 q= 41051.448 r= -43636.344 s= -1606.533
a=:a+:l*(:t-:e) b=:b+:m*(:t-:f) c=:c+:n*(:t-:g) d=:d+:o*(:t-:h)
:X=a/e+b/f+c/g+d/h-i :Y=a/j+b/k+c/l+d/m-n :Z=a/o+b/p+c/q+d/r-s goto 4
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
w=1000 x="\nX:" y="\nY:" z="\nZ:" 
:ISAN=x+:X/w*w+y+:Y/w*w+z+:Z/w*w goto 2
//--+----|----+----|----+----|----+----|----+----|----+----|----+----|
:ca=:l*(:t-:e) :cb=:m*(:t-:f) :cc=:n*(:t-:g) :cd=:o*(:t-:h) goto 1



//Global memory map
//a b c d e f g h i j k l m n o p q r s t u v w x y z
//|recvs| |times|       | rate| |REC|   t       |pos|
//                              m t s   
